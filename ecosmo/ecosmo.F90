#include "fabm_driver.h"

!-----------------------------------------------------------------------
!BOP
!
! !MODULE: fabm_nersc_ecosmo --- ECOSMO biogeochemical model
!
! !INTERFACE:
   module fabm_nersc_ecosmo
!
! !DESCRIPTION:
!
! The ECOSMO model is based on Daewel & Schrum (JMS,2013)
!
! !USES:
   use fabm_types
   use fabm_expressions
   implicit none

!  default: all is private.
   private
!
! !PUBLIC MEMBER FUNCTIONS:
   public type_nersc_ecosmo
!
! !PRIVATE DATA MEMBERS:
   real(rk), parameter :: secs_pr_day = 86400.0_rk
   real(rk), parameter :: sedy0 = 86400.0_rk
   real(rk), parameter :: mmolm3_in_mll = 44.6608009_rk
   real(rk)            :: redf(20)=0.0_rk
   real(rk)            :: BioC(45)=0.0_rk
!
! !PUBLIC DERIVED TYPES:
   type,extends(type_base_model) :: type_nersc_ecosmo
!     Variable identifiers
      type (type_state_variable_id)         :: id_no3, id_nh4, id_pho, id_sil
      type (type_state_variable_id)         :: id_opa, id_det, id_dia, id_fla
      type (type_state_variable_id)         :: id_diachl, id_flachl, id_bgchl, id_coccochl
      type (type_state_variable_id)         :: id_mesozoo, id_microzoo, id_bg,id_dom, id_oxy
      type (type_state_variable_id)         :: id_dic, id_alk
      type (type_bottom_state_variable_id)  :: id_sed1, id_sed2, id_sed3
      type (type_dependency_id)             :: id_temp, id_salt, id_par
      type (type_dependency_id)             :: id_parmean
      type (type_horizontal_dependency_id)  :: id_tbs
      type (type_horizontal_dependency_id)  :: id_sfpar, id_meansfpar
      type (type_diagnostic_variable_id)    :: id_denit, id_primprod, id_secprod, id_netpp
      type (type_diagnostic_variable_id)    :: id_parmean_diag
      type (type_diagnostic_variable_id)    :: id_c2chl_fla, id_c2chl_dia,id_c2chl_bg,id_c2chl_cocco
      type (type_diagnostic_variable_id)    :: id_nlim, id_plim, id_slim, id_llim
      type (type_horizontal_diagnostic_variable_id)    :: id_tbsout
      ! community sinking id's
      type (type_state_variable_id)         :: id_dsnk ! the variable that advects detritus sinking speeds
      type (type_diagnostic_variable_id)    :: id_snkspd ! simulated average sinking speed
      ! coccolithophores + caco3 
      type (type_state_variable_id)         :: id_cocco, id_caco3
      type (type_dependency_id)             :: id_Om_cal
      type (type_bottom_state_variable_id)  :: id_sed4 ! CaCO3 in sediment

!     Model parameters
      real(rk) :: BioC(45)
      real(rk) :: extdet, extdom
      real(rk) :: zpr, frr
      real(rk) :: prefZsPs
      real(rk) :: prefZsPl
      real(rk) :: prefZsBG
      real(rk) :: prefZsD
      real(rk) :: prefZlPs
      real(rk) :: prefZlPl
      real(rk) :: prefZlBG
      real(rk) :: prefZlD
      real(rk) :: prefZlZs
      real(rk) :: surface_deposition_no3
      real(rk) :: surface_deposition_nh4
      real(rk) :: surface_deposition_pho
      real(rk) :: surface_deposition_sil
      real(rk) :: nfixation_minimum_daily_par
      real(rk) :: bg_growth_minimum_daily_rad
      real(rk) :: MAXchl2nPs, MINchl2nPs 
      real(rk) :: MAXchl2nPl, MINchl2nPl 
      real(rk) :: MAXchl2nBG, MINchl2nBG 
      real(rk) :: alfaPl, alfaPs, alfaBG
      ! 2024 additions
      real(rk) :: m2Pl, m2Ps, m2Zl, m2Zs, mort2Cocco
      real(rk) :: Km2Pl, Km2Ps, Km2Zl, Km2Zs, Km2Cocco
      real(rk) :: RgZl
      ! 
      ! community dependent sinking parameters
      real(rk) :: sinkDiaD,sinkFlaD,sinkMicD,sinkMesD,sinkBgD, sinkCoccoD
      ! coccolithophore + caco3 parameters
      real(rk) :: prefZsCocco, prefZlCocco
      real(rk) :: muCocco, mortCocco
      real(rk) :: alfaCocco, MAXchl2nCocco, MINchl2nCocco, aaCocco
      real(rk) :: rNH4cocco, rNO3cocco, rPO4cocco
      real(rk) :: GrZlCocco, GrZsCocco
      real(rk) :: sinkCocco
      real(rk) :: incCarbR, calcR, calcDis
      real(rk) :: SiUptLim

      ! ECOSMO modules
      logical  :: use_chl ! activates explicit chlorophyll-a variable
      logical  :: use_cyanos ! activates cyanobacteria, effective for low salinity conditions
      logical  :: couple_co2 ! activates PML (Blackford, 2004) carbon module
      logical  :: not_0d ! module to run the model in 0d
      logical  :: use_community_sinking ! activates community composition dependent sinking rates
      logical  :: use_chl_in_PI_curve ! activated chl dependent light limitation (temporary now - will be permanent)
      logical  :: turn_on_additional_diagnostics ! activates additional diagnostics for model debugging
      logical  :: use_coccolithophores ! activates coccolithophores 
      
      contains

!     Model procedures
      procedure :: initialize
      procedure :: do
      procedure :: do_surface
      procedure :: do_bottom
      procedure :: get_light_extinction
      procedure :: get_vertical_movement      

   end type type_nersc_ecosmo
!EOP
!-----------------------------------------------------------------------
   type (type_bulk_standard_variable), parameter :: total_chlorophyll = type_bulk_standard_variable(name='total_chlorophyll',units='mg/m^3',aggregate_variable=.true.)
   contains

!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: Initialise the ECOSMO model
!
! !INTERFACE:
   subroutine initialize(self,configunit)
!
! !INPUT PARAMETERS:
   class (type_nersc_ecosmo),intent(inout),target  :: self
   integer,                intent(in)            :: configunit
!
! !REVISION HISTORY:
!  Original author(s): Richard Hofmeister
!
!  Caglar Yumruktepe:
!     Added fabm.yaml support: parameters from yaml file are copied to
!                           BioC array. Eventually, BioC array will be removed
!                           from the model where parameter names from the yaml
!                           file will be used.
!     Added dynamic chlorophyll-a from Geider etal., 1997
!     Added community dependent particle sinking rates
!     Added chlorophyll-a dependent light-limitation   
!
! !LOCAL VARIABLES:
! Everything else taken from yaml file
!
   integer :: i
   ! set Redfield ratios:
   redf(1) = 6.625_rk      !C_N
   redf(2) = 106.0_rk      !C_P
   redf(3) = 6.625_rk      !C_SiO
   redf(4) = 16.0_rk       !N_P
   redf(5) = 1.0_rk        !N_SiO
   redf(6) = 12.01_rk      !C_Cmg
   redf(7) = 44.6608009_rk !O2mm_ml
   redf(8) = 14.007_rk     !N_Nmg
   redf(9) = 30.97_rk      !P_Pmg
   redf(10) = 28.09_rk     !Si_Simg
   do i=1,10
     redf(i+10) = 1._rk/redf(i)
   end do


!EOP
!-----------------------------------------------------------------------
!BOC

   call self%get_parameter(self%zpr, 'zpr', '1/day', 'zpr_long_name_needed', default=0.001_rk, scale_factor=1.0_rk/sedy0)
   call self%get_parameter(self%frr, 'frr', '-', 'fraction of dissolved from det.', default=0.4_rk)
   call self%get_parameter(self%nfixation_minimum_daily_par, 'nfixation_minimum_daily_par', 'nfixation minimum daily par', default=40.0_rk)
   call self%get_parameter(self%bg_growth_minimum_daily_rad, 'bg_growth_minimum_daily_rad', 'bg growth minimum daily rad', default=120.0_rk)
   ! set surface fluxes in [mgC/m2/s]
   call self%get_parameter( self%surface_deposition_no3, 'surface_deposition_no3', 'mmolN/m**2 d', 'surface deposition no3', default=0.0_rk, scale_factor=redf(1)*redf(6)/sedy0 )
   call self%get_parameter( self%surface_deposition_nh4, 'surface_deposition_nh4', 'mmolN/m**2 d', 'surface deposition nh4', default=0.0_rk, scale_factor=redf(1)*redf(6)/sedy0 )
   call self%get_parameter( self%surface_deposition_pho, 'surface_deposition_pho', 'mmolP/m**2 d', 'surface deposition pho', default=0.0_rk, scale_factor=redf(2)*redf(6)/sedy0 )
   call self%get_parameter( self%surface_deposition_sil, 'surface_deposition_sil', 'mmolSi/m**2 d', 'surface deposition sil', default=0.0_rk, scale_factor=redf(3)*redf(6)/sedy0 )
   !  change units 1/day to 1/sec and mmolN,P,Si to mmolC
   call self%get_parameter( self%BioC(1) , 'muPl',        '1/day',      'max growth rate for Pl',          default=1.30_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(2) , 'muPs',        '1/day',      'max growth rate for Ps',          default=1.10_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(3) , 'aa',          'm**2/W',     'photosynthesis ef-cy',            default=0.04_rk)
   call self%get_parameter( self%BioC(4) , 'EXw',         '1/m',        'light extinction',                default=0.041_rk)
   if (self%use_chl) then
      call self%get_parameter( self%BioC(5) , 'Exphy',       'm**2/mgCHL', 'phyto self-shading',              default=0.04_rk )
   else
      call self%get_parameter( self%BioC(5) , 'Exphy',       'm**2/mmolN', 'phyto self-shading',              default=0.04_rk, scale_factor=1.0_rk/(redf(1)*redf(6)) )
   end if
   call self%get_parameter( self%extdet , 'Exdet',       'm**2/molC', 'detritus self-shading',         default=0.0_rk, scale_factor=1.0_rk/1000.0_rk )
   call self%get_parameter( self%extdom , 'Exdom',       'm**2/molC', 'dom self-shading',              default=0.0_rk, scale_factor=1.0_rk/1000.0_rk )
   call self%get_parameter( self%BioC(6) , 'rNH4',        'mmolN/m**3', 'NH4 half saturation',             default=0.20_rk,  scale_factor=redf(1)*redf(6))
   call self%get_parameter( self%BioC(7) , 'rNO3',        'mmolN/m**3', 'NO3 half saturation',             default=0.50_rk,  scale_factor=redf(1)*redf(6))
   call self%get_parameter( self%BioC(8) , 'psi',         'm**3/mmolN', 'NH4 inhibition',                  default=3.0_rk,   scale_factor=1.0_rk/(redf(1)*redf(6)) )
   call self%get_parameter( self%BioC(9) , 'mPl',         '1/day',      'Pl mortality rate',               default=0.04_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%m2Pl , 'm2Pl',         '1/day',      'Pl higher mortality rate',               default=0.0_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%Km2Pl, 'Km2Pl',         'mgC/m**3',      'half saturation for Pl higher mortality rate', default=150.0_rk)
   call self%get_parameter( self%BioC(10), 'mPs',         '1/day',      'Ps mortality rate',               default=0.08_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%m2Ps, 'm2Ps',         '1/day',      'Ps higher mortality rate',               default=0.0_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%Km2Ps, 'Km2Ps',         'mgC/m**3',      'half saturation for Ps higher mortality rate', default=150.0_rk)
   call self%get_parameter( self%BioC(11), 'GrZlP',       '1/day',      'Grazing rate Zl on Phyto',        default=0.80_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(12), 'GrZsP',       '1/day',      'Grazing rate Zs on Phyto',        default=1.00_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(13), 'GrZlZ',       '1/day',      'Grazing rate Zl on Zs',           default=0.50_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(14), 'RgZs',          'mmolN/m**3', 'Zs half saturation',          default=0.50_rk,  scale_factor=redf(1)*redf(6))
   call self%get_parameter( self%RgZl, 'RgZl',          'mmolN/m**3', 'Zl half saturation',          default=0.25_rk,  scale_factor=redf(1)*redf(6))
   call self%get_parameter( self%BioC(15), 'mZl',         '1/day',      'Zl mortality rate',               default=0.10_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%m2Zl, 'm2Zl',         '1/day',      'Zl higher mortality rate',               default=0.0_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%Km2Zl, 'Km2Zl',         'mgC/m**3',      'half saturation for Zl higher mortality rate', default=150.0_rk)
   call self%get_parameter( self%BioC(16), 'mZs',         '1/day',      'Zs mortality rate',               default=0.20_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%m2Zs, 'm2Zs',         '1/day',      'Zs higher mortality rate',               default=0.0_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%Km2Ps, 'Km2Zs',         'mgC/m**3',      'half saturation for Zs higher mortality rate', default=150.0_rk)
   call self%get_parameter( self%BioC(17), 'excZl',       '1/day',      'Zl excretion rate',               default=0.06_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(18), 'excZs',       '1/day',      'Zs excretion rate',               default=0.08_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(19), 'gammaZlp',    '1',          'Zl assim. eff. on plankton',      default=0.75_rk)
   call self%get_parameter( self%BioC(20), 'gammaZsp',    '1',          'Zs assim. eff. on plankton',      default=0.75_rk)
   call self%get_parameter( self%BioC(21), 'gammaZd',     '1',          'Zl & Zs assim. eff. on det',      default=0.75_rk)
   call self%get_parameter( self%BioC(22), 'reminD',      '1/day',      'Detritus remin. rate',            default=0.003_rk, scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(23), 'sinkDet',     'm/day',      'Detritus sinking rate',           default=5.00_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(24), 'Wa',          'm/day',      '???',                             default=1.00_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(25),  'rPO4',       'mmolP/m**3', 'PO4 half saturation',             default=0.05_rk,  scale_factor=redf(2)*redf(6))
   call self%get_parameter( self%BioC(26),  'rSi',        'mmolSi/m**3','SiO2 half saturation',            default=0.50_rk,  scale_factor=redf(3)*redf(6))
   call self%get_parameter( self%BioC(27),  'regenSi',    '1/day',      'Si regeneration rate',            default=0.015_rk, scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(28),  'muBG',       '1/day',      'max growth rate for BG',          default=1.00_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(29),  'TctrlBG',    '1/degC',     'BG T control beta',               default=1.00_rk)
   call self%get_parameter( self%BioC(30),  'TrefBG',     'degC',       'BG reference temperature',        default=0.00_rk)
   call self%get_parameter( self%BioC(31),  'GrBG',       '1/day',      'BG max grazing rate',             default=0.30_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(32),  'mBG',        '1/day',      'BG mortality rate',               default=0.08_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(33),  'upliftBG',   'm/day',      'BG uplifting rate',               default=0.10_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(34),  'crBotStr',   'N/m**2',     'critic. bot. stress for resusp.', default=0.1_rk)
   call self%get_parameter( self%BioC(35),  'resuspRt',   '1/day',      'resuspension rate',               default=25.0_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(36),  'sedimRt',    'm/day',      'sedimentation rate',              default=3.5_rk,   scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(37),  'burialRt',   '1/day',      'burial rate',                     default=1E-5_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(38),  'reminSED',   '1/day',      'sediment remineralization rate',  default=0.001_rk, scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(39),  'TctrlDenit', '1/degC',     'temp. control denitrification',   default=0.15_rk)
   call self%get_parameter( self%BioC(40),  'RelSEDp1',   'units??',    'P sedim. rel. p1',                default=0.15_rk)
   call self%get_parameter( self%BioC(41),  'RelSEDp2',   'units??',    'P sedim. rel. p2',                default=0.10_rk)
   call self%get_parameter( self%BioC(42),  'reminSEDsi', '1/day',      'sed. remineralization rate Si',   default=0.0002_rk,scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(43),  'sinkOPAL',   'm/day',      'OPAL sinking rate',               default=5.0_rk,   scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(44),  'sinkBG',     'm/day',      'BG sinking rate',                 default=-1.0_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%BioC(45),  'sinkDia',    'm/day',      'Diatom sinking rate',             default=0.0_rk,   scale_factor=1.0_rk/sedy0)
   !  growth fractions
   call self%get_parameter( self%prefZsPs,  'prefZsPs',   '-',          'Grazing preference Zs on Ps',     default=0.70_rk)
   call self%get_parameter( self%prefZsPl,  'prefZsPl',   '-',          'Grazing preference Zs on Pl',     default=0.25_rk)
   call self%get_parameter( self%prefZsD,   'prefZsD',    '-',          'Grazing preference Zs on Det.',   default=0.00_rk)
   call self%get_parameter( self%prefZsBG,  'prefZsBG',   '-',          'Grazing preference Zs on BG',     default=0.30_rk)
   call self%get_parameter( self%prefZlPs,  'prefZlPs',   '-',          'Grazing preference Zl on Ps',     default=0.10_rk)
   call self%get_parameter( self%prefZlPl,  'prefZlPl',   '-',          'Grazing preference Zl on Pl',     default=0.85_rk)
   call self%get_parameter( self%prefZlZs,  'prefZlZs',   '-',          'Grazing preference Zl on Zs',     default=0.15_rk)
   call self%get_parameter( self%prefZlD,   'prefZlD',    '-',          'Grazing preference Zl on Det.',   default=0.00_rk)
   call self%get_parameter( self%prefZlBG,  'prefZlBG',   '-',          'Grazing preference Zl on BG',     default=0.30_rk)
   call self%get_parameter( self%prefZsCocco,  'prefZsCocco',   '-',    'Grazing preference Zs on Cocco',  default=0.70_rk)
   call self%get_parameter( self%prefZlCocco,  'prefZlCocco',   '-',    'Grazing preference Zl on Cocco',  default=0.10_rk)
   ! coccolithophore + caco3 parameters
   call self%get_parameter( self%muCocco , 'muCocco',        '1/day',      'max growth rate for Coccolithophores',  default=1.10_rk,  scale_factor=1.0_rk/sedy0) 
   call self%get_parameter( self%mortCocco , 'mortCocco',         '1/day',      'Coccolithophore mortality rate',  default=0.04_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%mort2Cocco, 'mort2Cocco',         '1/day',      'Coccolithophores higher mortality rate', default=0.0_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%Km2Cocco, 'Km2Cocco',         'mgC/m**3',      'half saturation for Coccolithophores higher mortality rate', default=150.0_rk)
   call self%get_parameter( self%rNH4cocco , 'rNH4cocco',   'mmolN/m**3', 'NH4 half saturation for Coccolithophores',  default=0.20_rk,  scale_factor=redf(1)*redf(6))
   call self%get_parameter( self%rNO3cocco , 'rNO3cocco',    'mmolN/m**3', 'NO3 half saturation for Coccolithophores', default=1.0_rk,  scale_factor=redf(1)*redf(6))
   call self%get_parameter( self%rPO4cocco,  'rPO4cocco',    'mmolP/m**3', 'PO4 half saturation for Coccolithophores', default=0.0015_rk,  scale_factor=redf(2)*redf(6))
   call self%get_parameter( self%aaCocco , 'aaCocco',        'm**2/W',     'photosynthesis ef-cy for Coccolithophores',  default=0.012_rk)
   call self%get_parameter( self%GrZlCocco, 'GrZlCocco',       '1/day',      'Grazing rate Zl on Cocco',        default=0.80_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%GrZsCocco, 'GrZsCocco',       '1/day',      'Grazing rate Zs on Cocco',        default=1.00_rk,  scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%sinkCocco,  'sinkCocco',    'm/day',      'Coccolithophore sinking rate',             default=0.0_rk,   scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%incCarbR , 'incCarbR',        'TBF',      'increa carb.rat cocc',  default=1.42_rk) 
   call self%get_parameter( self%calcR , 'calcR',        '-',      'Maximum calcification to organic carbon production RCaCO3',  default=0.4_rk)
   call self%get_parameter( self%calcDis , 'calcDis',        '1/day',      'calcite dissolution rate',  default=0.03_rk,  scale_factor=1.0_rk/sedy0)
 
   ! chlorophyll-a constants for dia and fla 
   call self%get_parameter( self%MINchl2nPs, 'MINchl2nPs', 'mgChl/mmolN', 'minimum Chl to N ratio Ps', default=0.50_rk, scale_factor=redf(11)*redf(16))
   call self%get_parameter( self%MAXchl2nPs, 'MAXchl2nPs', 'mgChl/mmolN', 'maximum Chl to N ratio Ps', default=3.83_rk, scale_factor=redf(11)*redf(16))
   call self%get_parameter( self%MINchl2nPl, 'MINchl2nPl', 'mgChl/mmolN', 'minimum Chl to N ratio Pl', default=0.50_rk, scale_factor=redf(11)*redf(16))
   call self%get_parameter( self%MAXchl2nPl, 'MAXchl2nPl', 'mgChl/mmolN', 'maximum Chl to N ratio Pl', default=2.94_rk, scale_factor=redf(11)*redf(16))
   call self%get_parameter( self%MINchl2nBG, 'MINchl2nBG', 'mgChl/mmolN', 'minimum Chl to N ratio BG', default=0.50_rk, scale_factor=redf(11)*redf(16))
   call self%get_parameter( self%MAXchl2nBG, 'MAXchl2nBG', 'mgChl/mmolN', 'maximum Chl to N ratio BG', default=3.83_rk, scale_factor=redf(11)*redf(16))
   call self%get_parameter( self%MINchl2nCocco, 'MINchl2nCocco', 'mgChl/mmolN', 'minimum Chl to N ratio Cocco', default=0.50_rk, scale_factor=redf(11)*redf(16))
   call self%get_parameter( self%MAXchl2nCocco, 'MAXchl2nCocco', 'mgChl/mmolN', 'maximum Chl to N ratio Cocco', default=3.83_rk, scale_factor=redf(11)*redf(16))
   call self%get_parameter( self%alfaPs,     'alfaPs', 'mmolN m2/(mgChl day W)**-1', 'initial slope P-I curve Ps', default=0.0393_rk, scale_factor=redf(1)*redf(6) )
   call self%get_parameter( self%alfaPl,     'alfaPl', 'mmolN m2/(mgChl day W)**-1', 'initial slope P-I curve Pl', default=0.0531_rk, scale_factor=redf(1)*redf(6) )
   call self%get_parameter( self%alfaBG,     'alfaBG', 'mmolN m2/(mgChl day W)**-1', 'initial slope P-I curve BG', default=0.0393_rk, scale_factor=redf(1)*redf(6) )
   call self%get_parameter( self%alfaCocco,     'alfaCocco', 'mmolN m2/(mgChl day W)**-1', 'initial slope P-I curve Cocco', default=0.0393_rk, scale_factor=redf(1)*redf(6) )
  ! community sinking constants
   call self%get_parameter( self%sinkDiaD,  'sinkDiaD',    'm/day', 'Detritus originating from diatom sinking rate',       default=5.0_rk, scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%sinkFlaD,  'sinkFlaD',    'm/day', 'Detritus originating from flagellates sinking rate',  default=5.0_rk, scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%sinkMicD,  'sinkMicD',    'm/day', 'Detritus originating from microzoo sinking rate',     default=5.0_rk, scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%sinkMesD,  'sinkMesD',    'm/day', 'Detritus originating from mesozoo sinking rate',      default=5.0_rk, scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%sinkBgD,   'sinkBgD',     'm/day', 'Detritus originating from cyanob. sinking rate',      default=5.0_rk, scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%sinkCoccoD,  'sinkCoccoD',   'm/day', 'Detritus originating from coccolith. sinking rate',  default=5.0_rk, scale_factor=1.0_rk/sedy0)
   call self%get_parameter( self%SiUptLim,  'SiUptLim',   'mgC/m3', 'Stop Si uptake below this concentration',  default=80.0_rk)
   ! add switches
   call self%get_parameter( self%use_cyanos,     'use_cyanos', '', 'switch cyanobacteria', default=.true.)
   call self%get_parameter( self%couple_co2,     'couple_co2', '', 'switch coupling to carbonate module', default=.false.)
   call self%get_parameter( self%use_chl,     'use_chl', '', 'switch chlorophyll/c dynamics', default=.true.)
   call self%get_parameter( self%not_0d,     'not_0d', '', 'do not run the model in a 0D box', default=.true.)
   call self%get_parameter( self%use_community_sinking, 'use_community_sinking','','community composition dependent sinking rates', default=.false.)   
   call self%get_parameter( self%use_chl_in_PI_curve, 'use_chl_in_PI_curve','','activated chl dependent light limitation',default=.false.)
   call self%get_parameter( self%turn_on_additional_diagnostics, 'turn_on_additional_diagnostics','','activates additional diagnostics for model debugging',default=.false.)
   call self%get_parameter( self%use_coccolithophores,     'use_coccolithophores', '', 'switch coccolithophores', default=.false.)
   ! Register state variables
   call self%register_state_variable( self%id_no3,      'no3',     'mgC/m3',    'nitrate',                   minimum=0.0_rk,        vertical_movement=0.0_rk,  &
                                      initial_value=5.0_rk*redf(1)*redf(6)  )
   call self%register_state_variable( self%id_nh4,      'nh4',     'mgC/m3',    'ammonium',                  minimum=0.0_rk,        vertical_movement=0.0_rk,  &
                                      initial_value=0.1_rk*redf(1)*redf(6)  )
   call self%register_state_variable( self%id_pho,      'pho',     'mgC/m3',    'phosphate',                 minimum=0.0_rk,        vertical_movement=0.0_rk,  &
                                      initial_value=0.3_rk*redf(2)*redf(6)  )
   call self%register_state_variable( self%id_sil,      'sil',     'mgC/m3',    'silicate',                  minimum=0.0_rk,        vertical_movement=0.0_rk,  &
                                      initial_value=5.0_rk*redf(3)*redf(6)  )
   call self%register_state_variable( self%id_oxy,      'oxy',     'mmolO2/m3', 'oxygen',                    minimum=0.0_rk,   vertical_movement=0.0_rk,  &
                                      initial_value=85.0_rk  )
   call self%register_state_variable( self%id_fla,      'fla',     'mgC/m3',    'small phytoplankton',       minimum=1.0e-7_rk,     vertical_movement=0.0_rk, &
                                      initial_value=1e-4_rk*redf(1)*redf(6))
   call self%register_state_variable( self%id_dia,      'dia',     'mgC/m3',    'large phytoplankton',       minimum=1.0e-7_rk,     vertical_movement=-self%BioC(45) , &
                                      initial_value=1e-4_rk*redf(1)*redf(6) )
   if (self%use_cyanos) then
     call self%register_state_variable( self%id_bg,       'bg',      'mgC/m3',    'cyanobacteria',             minimum=1.0e-14_rk,     vertical_movement=-self%BioC(44) , &
                                      initial_value=1e-4_rk*redf(1)*redf(6) )
     if (self%use_chl) then
       call self%register_state_variable( self%id_bgchl,    'bgchl',   'mgChl/m3',  'cyanobacteria chl-a',       minimum=1.0e-14_rk/20., vertical_movement=-self%BioC(44) , &
                                      initial_value=1e-4_rk*redf(1)*redf(6)/27.)
       call self%add_to_aggregate_variable(total_chlorophyll, self%id_bgchl)
     else
       call self%add_to_aggregate_variable(total_chlorophyll, self%id_bg, scale_factor=1.0_rk/60.0_rk)
     end if
   end if
   if (self%use_coccolithophores) then
     call self%register_state_variable( self%id_cocco,       'ccl',      'mgC/m3',    'coccolithophores',             minimum=1.0e-14_rk,     vertical_movement=-self%sinkCocco , &
                                      initial_value=1e-4_rk*redf(1)*redf(6) )
     if (self%use_chl) then
       call self%register_state_variable( self%id_coccochl,    'cclchl',   'mgChl/m3',  'coccolithophores chl-a',       minimum=1.0e-14_rk/20., vertical_movement=-self%sinkCocco , &
                                      initial_value=1e-4_rk*redf(1)*redf(6)/27.)
       call self%add_to_aggregate_variable(total_chlorophyll, self%id_coccochl)
     else
       call self%add_to_aggregate_variable(total_chlorophyll, self%id_cocco, scale_factor=1.0_rk/60.0_rk)
     end if
     call self%register_state_variable( self%id_caco3,       'caco3',      'mmol/m3',    'calcite',             minimum=1.0e-14_rk,      &
                                      initial_value=1e-3_rk )
     call self%register_state_variable( self%id_sed4,     'sed4',    'mmol/m2',    'sediment calcite',         minimum=0.0_rk , &
                                      initial_value=1e-2_rk*redf(6),maximum=10000.0_rk )      
   end if
   if (self%use_chl) then
     call self%register_state_variable( self%id_diachl,   'diachl',  'mgChl/m3',  'large phytoplankton chl-a', minimum=1.0e-7_rk/27., vertical_movement=-self%BioC(45) , &
                                      initial_value=1e-4_rk*redf(1)*redf(6)/27.)
     call self%register_state_variable( self%id_flachl,   'flachl',  'mgChl/m3',  'small phytoplankton chl-a', minimum=1.0e-7_rk/20., vertical_movement=0.0_rk, &
                                      initial_value=1e-4_rk*redf(1)*redf(6)/20.)
     call self%add_to_aggregate_variable(total_chlorophyll, self%id_diachl)
     call self%add_to_aggregate_variable(total_chlorophyll, self%id_flachl)
   else
      call self%add_to_aggregate_variable(total_chlorophyll, self%id_dia, scale_factor=1.0_rk/60.0_rk)
      call self%add_to_aggregate_variable(total_chlorophyll, self%id_fla, scale_factor=1.0_rk/60.0_rk)
   end if
   call self%register_state_variable( self%id_microzoo, 'microzoo','mgC/m3',    'microzooplankton',          minimum=1.0e-7_rk,     vertical_movement=0.0_rk, &
                                      initial_value=1e-6_rk*redf(1)*redf(6) )
   call self%register_state_variable( self%id_mesozoo,  'mesozoo', 'mgC/m3',    'mesozooplankton',           minimum=1.0e-7_rk,     vertical_movement=0.0_rk, &
                                      initial_value=1e-6_rk*redf(1)*redf(6) )
   call self%register_state_variable( self%id_det,      'det',     'mgC/m3',    'detritus',                  minimum=1.0e-10_rk,         &
                                      initial_value=2.0_rk*redf(1)*redf(6)  )
   call self%register_state_variable( self%id_opa,      'opa',     'mgC/m3',    'opal',                      minimum=0.0_rk,  &
                                      initial_value=2.0_rk*redf(3)*redf(6) )
   call self%register_state_variable( self%id_dom,      'dom',     'mgC/m3',    'labile dissolved om',       minimum=0.0_rk , &
                                      initial_value=3.0_rk*redf(1)*redf(6)   )
   call self%register_state_variable( self%id_sed1,     'sed1',    'mgC/m2',    'sediment detritus',         minimum=0.0_rk , &
                                      initial_value=20.0_rk*redf(1)*redf(6)*redf(18), maximum=10000.0_rk )
   call self%register_state_variable( self%id_sed2,     'sed2',    'mgC/m2',    'sediment opal',             minimum=0.0_rk , &
                                      initial_value=20.0_rk*redf(3)*redf(6)*redf(20), maximum=10000.0_rk )
   call self%register_state_variable( self%id_sed3,     'sed3',    'mgC/m2',    'sediment adsorbed pho.',    minimum=0.0_rk , &
                                      initial_value=2.0_rk*redf(2)*redf(6)*redf(19), maximum=10000.0_rk )
   ! community sinking speed state variable
   if (self%use_community_sinking) then
      call self%register_state_variable( self%id_dsnk,      'dsnk',    'mgC/m3', 'detritus sinking speed advector', minimum=1.0e-10_rk/sedy0 , &
               initial_value=2.0_rk*redf(1)*redf(6)*self%BioC(23))
   end if
   ! Register diagnostic variables
   call self%register_diagnostic_variable(self%id_primprod,'primprod','mgC/m**3/s', &
         'gross primary production rate', output=output_time_step_averaged)
   call self%register_diagnostic_variable(self%id_secprod,'secprod','mgC/m**3/s', &
         'secondary production rate', output=output_time_step_averaged)
   call self%register_diagnostic_variable(self%id_netpp,'netpp','mgC/m**3/s', &
         'net primary production rate', output=output_time_step_averaged)

   if (self%turn_on_additional_diagnostics) then  
         call self%register_diagnostic_variable(self%id_parmean_diag,'parmean','W/m**2', &
         'daily-mean photosynthetically active radiation', output=output_time_step_averaged)
         call self%register_diagnostic_variable(self%id_nlim,'Nlim','-', &
         'N-limitation on primary production', output=output_time_step_averaged)
         call self%register_diagnostic_variable(self%id_plim,'Plim','-', &
         'P-limitation on primary production', output=output_time_step_averaged)
         call self%register_diagnostic_variable(self%id_slim,'Slim','-', &
         'Si-limitation on primary production', output=output_time_step_averaged)
         call self%register_diagnostic_variable(self%id_llim,'Llim','-', &
         'Light-limitation on primary production', output=output_time_step_averaged)
         call self%register_diagnostic_variable(self%id_denit,'denit','mmolN/m**3/s', &
         'denitrification rate', output=output_time_step_averaged)
         call self%register_diagnostic_variable(self%id_tbsout,'botstrss','fill_later', &
         'total bottom stress', source=source_do_bottom)
         if (self%use_community_sinking) then
            call self%register_diagnostic_variable(self%id_snkspd,'snkspd','m/d', &
               'daily-mean detritus sinking speed', output=output_time_step_averaged)
         end if
         if (self%use_chl) then
          call self%register_diagnostic_variable(self%id_c2chl_fla,'c2chl_fla','mgC/mgCHL', &
              'daily-mean C to CHL ratio for flagellates', output=output_time_step_averaged)
          call self%register_diagnostic_variable(self%id_c2chl_dia,'c2chl_dia','mgC/mgCHL', &
              'daily-mean C to CHL ratio for diatoms', output=output_time_step_averaged)
          if (self%use_cyanos) then
                  call self%register_diagnostic_variable(self%id_c2chl_bg,'c2chl_bg','mgC/mgCHL', &
                  'daily-mean C to CHL ratio for cyanobacteria', output=output_time_step_averaged)
          end if
          if (self%use_coccolithophores) then
            call self%register_diagnostic_variable(self%id_c2chl_cocco,'c2chl_cocco','mgC/mgCHL', &
            'daily-mean C to CHL ratio for coccolithophores', output=output_time_step_averaged)
          end if
         end if
   end if

   ! Register dependencies
   call self%register_dependency(self%id_temp,standard_variables%temperature)
   call self%register_dependency(self%id_salt,standard_variables%practical_salinity)
   call self%register_dependency(self%id_par,standard_variables%downwelling_photosynthetic_radiative_flux)
   if (self%not_0d) then
      call self%register_dependency(self%id_tbs,standard_variables%bottom_stress)
   end if 
   call self%register_dependency(self%id_sfpar,standard_variables%surface_downwelling_photosynthetic_radiative_flux)
   ! use temporal mean of light for the last 24 hours
   call self%register_dependency(self%id_parmean,temporal_mean(self%id_par,period=86400._rk,resolution=3600._rk))
   call self%register_dependency(self%id_meansfpar,temporal_mean(self%id_sfpar,period=86400._rk,resolution=3600._rk))

   if (self%couple_co2) then
     call self%register_state_dependency(self%id_dic, 'dic_target','mmol m-3','dic budget')
     call self%register_state_dependency(self%id_alk, 'alk_target','mmol m-3','alkalinity budget')
     if (self%use_coccolithophores) then
        call self%register_dependency(self%id_Om_cal,'Om_cal_target','-','calcite saturation')
     endif
   end if


!   call self%register_dependency(self%id_h,       'icethickness', 'm',    'ice thickness')
!   call self%register_dependency(self%id_hs,      'snowthickness','m',    'snow thickness')
   

   return

end subroutine initialize
!EOC

!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: Right hand sides of ECOSMO model
!
! !INTERFACE:
   subroutine do(self,_ARGUMENTS_DO_)
!
! !DESCRIPTION:
!
! !INPUT PARAMETERS:
   class (type_nersc_ecosmo),intent(in) :: self
   _DECLARE_ARGUMENTS_DO_
!
! !REVISION HISTORY:
!  Original author(s): Richard Hofmeister
!
! !LOCAL VARIABLES:
   real(rk) :: no3,nh4,pho,sil,t_sil,oxy,fla,dia
   real(rk) :: flachl,diachl,chl2c_fla,chl2c_dia
   real(rk) :: microzoo,mesozoo,opa,det,dom
   real(rk) :: temp,salt,par
   real(rk) :: frem, fremDOM, blight
   real(rk) :: Ts=1.0_rk
   real(rk) :: Tl=1.0_rk
   real(rk) :: Prod_Dia_Fla,Ps_prod,Pl_prod
   real(rk) :: Fs,Fl,ZlonPs,ZlonPl,ZsonD,ZlonD,ZsonPs,ZsonPl,ZlonZs
   real(rk) :: up_no3,up_nh4,up_n,up_pho,up_sil
   real(rk) :: bioom1,bioom2,bioom3,bioom4,bioom5,bioom6,bioom7,bioom8,Onitr
   real(rk) :: rhs,dxxdet,sinkdxxdet
   real(rk) :: Zl_prod, Zs_prod
   real(rk) :: mean_par, mean_surface_par
   real(rk) :: fla_loss=1.0_rk
   real(rk) :: dia_loss=1.0_rk
   real(rk) :: mic_loss=1.0_rk
   real(rk) :: mes_loss=1.0_rk
   real(rk) :: tbs
   real(rk) :: rhs_oxy,rhs_amm,rhs_nit

   ! 2024 additions
   real(rk) :: highMortPl, highMortPs, highMortZl, highMortZs, highMortCocco
   real(rk) :: sumgpp, sumnetpp
   !

! local variables for cyanobacteria
   real(rk) :: bg, bgchl, chl2c_bg
   real(rk) :: Bg_fix
   real(rk) :: ZlonBg, ZsonBg
   real(rk) :: Bg_prod, Tbg, Prod_BG
   real(rk) :: bg_loss=1.0_rk
!

! local variables for community sinking   
   real(rk) :: dsnk, mean_snkspd
!

! local variables for community specific P-I curves
   real(rk) :: blightDIA,blightFLA,blightCYA,blightCOCCO
!

! local variables for coccolithophores + caco3
   real(rk) :: cocco, coccochl, chl2c_cocco, caco3
   real(rk) :: Cocco_prod, Prod_Cocco
   real(rk) :: ZlonCocco, ZsonCocco
   real(rk) :: cocco_loss=1.0_rk
   real(rk) :: caco3_loss=1.0_rk
   real(rk) :: Tcocco=1.0_rk
   real(rk) :: up_nh4_cocco, up_no3_cocco, up_pho_cocco, up_n_cocco
   real(rk) :: Pcaco3, Rstar, Lstar, Om_cal


!EOP
!-----------------------------------------------------------------------
!BOC
   ! Enter spatial loops (if any)
   _LOOP_BEGIN_

   ! Retrieve current (local) state variable values.
   _GET_(self%id_temp,temp)
   _GET_(self%id_salt,salt)
   _GET_(self%id_par,par)
   _GET_(self%id_no3,no3)
   _GET_(self%id_nh4,nh4)
   _GET_(self%id_pho,pho)
   _GET_(self%id_sil,sil)
   _GET_(self%id_dia,dia)
   _GET_(self%id_fla,fla)
   if (self%use_cyanos) then
     _GET_(self%id_bg,bg)
   else
     bg=0.0_rk
   end if
   if (self%use_coccolithophores) then
     _GET_(self%id_cocco,cocco)
     _GET_(self%id_Om_cal,Om_cal)
   end if
   if (self%use_chl) then
     _GET_(self%id_diachl,diachl)
     _GET_(self%id_flachl,flachl)
     if (self%use_coccolithophores) then
       _GET_(self%id_coccochl,coccochl)
       _GET_(self%id_caco3,caco3)
     end if
     if (self%use_cyanos) then
       _GET_(self%id_bgchl,bgchl)
     end if
   end if
   if (self%use_community_sinking) then
    _GET_(self%id_dsnk,dsnk)
   end if
   _GET_(self%id_microzoo,microzoo)
   _GET_(self%id_mesozoo,mesozoo)
   _GET_(self%id_det,det)
   _GET_(self%id_dom,dom)
   _GET_(self%id_opa,opa)
   _GET_(self%id_oxy,oxy)
   _GET_(self%id_parmean,mean_par)
   _GET_HORIZONTAL_(self%id_meansfpar,mean_surface_par)
   _GET_HORIZONTAL_(self%id_tbs,tbs)

   ! CAGLAR
   ! checks - whether the biomass of plankton is below a predefined threshold,
   !          where below the threshold, loss terms are removed from the RHS of
   !          the equations. The idea is to keep plankton safe from extinction.
   ! loss terms are multiplied by the constants below, which can only be set
   ! by the model to 0 or 1.

   fla_loss   = max(sign(-1.0_rk,fla-0.5_rk),0.0_rk)       ! flagellates
   dia_loss   = max(sign(-1.0_rk,dia-0.5_rk),0.0_rk)       ! diatoms
   bg_loss    = max(sign(-1.0_rk,bg-0.5_rk),0.0_rk)        ! cyanobacteria
   mic_loss   = max(sign(-1.0_rk,microzoo-0.05_rk),0.0_rk) !microzooplankton
   mes_loss   = max(sign(-1.0_rk,mesozoo-0.05_rk),0.0_rk) ! mesozooplankton
   if (self%use_coccolithophores) then
      cocco_loss = max(sign(-1.0_rk,cocco-0.5_rk),0.0_rk)       ! coccolithophores
      caco3_loss = max(sign(-1.0_rk,caco3-0.1_rk),0.0_rk) ! caco3 - temporary for now
   end if

   ! remineralisation rate
   frem = self%BioC(22) * (1._rk+20._rk*(temp**2/(13._rk**2+temp**2)))
   fremDOM = 10.0_rk * frem

   ! temperature dependence
   if ((salt<=10.0) .and. (mean_surface_par > self%bg_growth_minimum_daily_rad)) then
    Tbg = 1.0_rk/(1.0_rk + exp(self%BioC(29)*(self%BioC(30)-temp)))
   else
    Tbg = 0.0_rk
   end if

   ! nutrient limitation factors
   ! k denotes half-saturation values
   up_nh4 = nh4/(self%BioC(6)+nh4)
   up_no3 = no3/(self%BioC(7)+no3)*exp(-self%BioC(8)*nh4)
   up_n = up_nh4+up_no3
   up_pho = pho/(self%BioC(25)+pho)
   t_sil = max(sil-self%SiUptLim,0.0_rk)
   up_sil = t_sil/(self%BioC(26)+t_sil)
   ! coccolithophore specific limitation
   if (self%use_coccolithophores) then
      up_nh4_cocco = nh4/(self%rNH4cocco+nh4)
      up_no3_cocco = no3/(self%rNO3cocco+no3)*exp(-self%BioC(8)*nh4)
      up_pho_cocco = pho/(self%rPO4cocco+pho)
      up_n_cocco = up_nh4_cocco+up_no3_cocco
   else
      up_nh4_cocco = 0.0_rk
      up_no3_cocco = 0.0_rk
      up_pho_cocco = 0.0_rk
      up_n_cocco = 0.0_rk 
   end if 

   ! production and nutrient uptake
   if (self%use_chl_in_PI_curve) then 
      blightFLA  = max( ((flachl/fla)*self%alfaPs*par) / sqrt((self%BioC(2)*sedy0)**2+(flachl/fla)**2*self%alfaPs**2*(par**2)) ,0.0_rk)
      blightDIA  = max( ((diachl/dia)*self%alfaPl*par) / sqrt((self%BioC(1)*sedy0)**2+(diachl/dia)**2*self%alfaPl**2*(par**2)) ,0.0_rk)
!      blightFLA  = max( ((flachl/fla)*self%alfaPs*par) / (self%BioC(2)*sedy0)+(flachl/fla)*self%alfaPs*(par) ,0.0_rk)
!      blightDIA  = max( ((diachl/dia)*self%alfaPl*par) / (self%BioC(1)*sedy0)+(diachl/dia)*self%alfaPl*(par) ,0.0_rk)

      Ps_prod = Ts * min(blightFLA, up_n, up_pho)
      Pl_prod = Tl * min(blightDIA, up_n, up_pho, up_sil)
      if (self%use_coccolithophores) then
         blightCOCCO  = max( ((coccochl/cocco)*self%alfaCocco*par) / sqrt((self%muCocco*sedy0)**2+(coccochl/cocco)**2*self%alfaCocco**2*(par**2)) ,0.0_rk)
!         blightCOCCO  = max( ((coccochl/cocco)*self%alfaCocco*par) / (self%muCocco*sedy0)+(coccochl/cocco)*self%alfaCocco*(par) ,0.0_rk)

         Cocco_prod = Tcocco * min(blightCOCCO, up_n_cocco, up_pho_cocco) 
      end if
   else
      ! light limitation - old formulation - default for now - CAGLAR:Sep2022
      blight = max(tanh(self%BioC(3)*par),0.0_rk)
      Ps_prod = Ts * min(blight, up_n, up_pho)
      Pl_prod = Tl * min(blight, up_n, up_pho, up_sil)  
      if (self%use_coccolithophores) then
         
         Cocco_prod = Tcocco * min(max(tanh(self%aaCocco*par),0.0_rk), up_n_cocco, up_pho_cocco)
      end if    
   end if
   
   Prod_Dia_Fla = self%BioC(1) * Pl_prod * dia + & ! diatoms production
          self%BioC(2) * Ps_prod * fla     ! flagellates production
   if (self%use_coccolithophores) then
      Prod_Cocco = self%muCocco * Cocco_prod * cocco ! coccolithophore production
   else
      Prod_Cocco = 0.0_rk
   end if

   if (self%use_cyanos) then
     Bg_prod = Tbg * min(blight, up_n, up_pho)
     if (mean_par > self%nfixation_minimum_daily_par) then
       Bg_fix = Tbg * min(blight, up_pho) - Bg_prod
     end if
     Prod_BG = self%BioC(28) * Bg_prod * bg ! cyanobacteria production
   else
     Prod_BG = 0.0_rk
   end if

   if (self%use_chl) then   
     ! chlorophyll-a to C change
     chl2c_fla = self%MAXchl2nPs * max(0.01,Ps_prod) * self%BioC(2) * sedy0 * fla / &
               (self%alfaPs * par * flachl)
     chl2c_dia = self%MAXchl2nPl * max(0.01,Pl_prod) * self%BioC(1) * sedy0 * dia / &
               (self%alfaPl * par * diachl)

     chl2c_fla = max(self%MINchl2nPs,chl2c_fla)
     chl2c_fla = min(self%MAXchl2nPs,chl2c_fla)
     chl2c_dia = max(self%MINchl2nPl,chl2c_dia)
     chl2c_dia = min(self%MAXchl2nPl,chl2c_dia)
     if (self%use_cyanos) then
       chl2c_bg = self%MAXchl2nBG * max(0.01,Bg_prod) * self%BioC(28) * sedy0 * bg / &
               (self%alfaBG * par * bgchl)
       chl2c_bg  = max(self%MINchl2nBG,chl2c_bg)
       chl2c_bg  = min(self%MAXchl2nBG,chl2c_bg)
     end if
     if (self%use_coccolithophores) then
        chl2c_cocco = self%MAXchl2nCocco * max(0.01,Cocco_prod) * self%muCocco * sedy0 * cocco / &
               (self%alfaCocco * par * coccochl)
        chl2c_cocco  = max(self%MINchl2nCocco,chl2c_cocco)
        chl2c_cocco  = min(self%MAXchl2nCocco,chl2c_cocco)
     end if 
   end if
   !if (1./chl2c_cocco>=100. .or. 1./chl2c_dia>=100. .or. 1./chl2c_fla>=100.) then 
   !write(*,*)1./chl2c_cocco,1./chl2c_fla,1./chl2c_dia
   !end if

! EXPERIMENTING WITH ZOOPLANKTON GRAZING PRESSURES
   ! grazing
!   Fs = self%prefZsPs*fla*(fla/(fla+12.)) + self%prefZsPl*dia*(dia/(dia+12.)) + self%prefZsD*det*(det/(det+12.)) + self%prefZsBG*bg*(bg/(bg+12.))
!   Fl = self%prefZlPs*fla*(fla/(fla+12.)) + self%prefZlPl*dia*(dia/(dia+12.)) + self%prefZlZs*microzoo*(microzoo/(microzoo+12.)) + &
!         self%prefZlD*det*(det/(det+12.)) + self%prefZlBG*bg*(bg/(bg+12.))
!   if (self%use_cyanos) then
!    Fs = Fs + self%prefZsBG*bg
!    Fl = Fl + self%prefZlBg*bg
!   end if
!
!   ZsonPs = fla_loss * self%BioC(12) * self%prefZsPs * fla*(fla/(fla+12.))/(self%BioC(14) + Fs)
!   ZsonPl = dia_loss * self%BioC(12) * self%prefZsPl * dia*(dia/(dia+12.))/(self%BioC(14) + Fs)
!   ZsonD  =            self%BioC(12) * self%prefZsD * det*(det/(det+12.))/(self%BioC(14) + Fs)
!
!   ZlonPs = fla_loss * self%BioC(11) * self%prefZlPs * fla*(fla/(fla+12.))/(self%BioC(14) + Fl)
!   ZlonPl = dia_loss * self%BioC(11) * self%prefZlPl * dia*(dia/(dia+12.))/(self%BioC(14) + Fl)
!   ZlonD =             self%BioC(11) * self%prefZlD * det*(det/(det+12.))/(self%BioC(14) + Fl)
!   ZlonZs = mic_loss * self%BioC(13) * self%prefZlZs * microzoo*(microzoo/(microzoo+12.))/(self%BioC(14) + Fl)
!   if (self%use_cyanos) then
!     ZsonBg = bg_loss  * self%BioC(31) * self%prefZsBG * bg/(self%BioC(14) + Fs)
!     ZlonBg = bg_loss  * self%BioC(31) * self%prefZlBG * bg/(self%BioC(14) + Fl)
!   else
!     ZsonBg=0.0_rk
!     ZlonBg=0.0_rk
!   end if
! EXPERIMENTAL

   Fs = self%prefZsPs*fla + self%prefZsPl*dia + self%prefZsD*det + self%prefZsBG*bg
   Fl = self%prefZlPs*fla + self%prefZlPl*dia + self%prefZlZs*microzoo + &
         self%prefZlD*det + self%prefZlBG*bg
   if (self%use_cyanos) then
    Fs = Fs + self%prefZsBG*bg
    Fl = Fl + self%prefZlBg*bg
   end if
   if (self%use_coccolithophores) then
    Fs = Fs + self%prefZsCocco*cocco
    Fl = Fl + self%prefZlCocco*cocco
   end if

   !ZsonPs = fla_loss * self%BioC(12) * self%prefZsPs * fla/(self%BioC(14) + Fs)
   !ZsonPl = dia_loss * self%BioC(12) * self%prefZsPl * dia/(self%BioC(14) + Fs)
   !ZsonD  =            self%BioC(12) * self%prefZsD * det/(self%BioC(14) + Fs)

   !ZlonPs = fla_loss * self%BioC(11) * self%prefZlPs * fla/(self%BioC(14) + Fl)
   !ZlonPl = dia_loss * self%BioC(11) * self%prefZlPl * dia/(self%BioC(14) + Fl)
   !ZlonD =             self%BioC(11) * self%prefZlD * det/(self%BioC(14) + Fl)
   !ZlonZs = mic_loss * self%BioC(13) * self%prefZlZs * microzoo/(self%BioC(14) + Fl)

   ! EXPERIMENT
   ZsonPs = fla_loss * self%BioC(12) * self%prefZsPs * fla**2/(self%BioC(14)**2 + Fs**2)
   ZsonPl = dia_loss * self%BioC(12) * self%prefZsPl * dia**2/(self%BioC(14)**2 + Fs**2)
   ZsonD  =            self%BioC(12) * self%prefZsD * det**2/(self%BioC(14)**2 + Fs**2)

   ZlonPs = fla_loss * self%BioC(11) * self%prefZlPs * fla**2/(self%RgZl**2 + Fl**2)
   ZlonPl = dia_loss * self%BioC(11) * self%prefZlPl * dia**2/(self%RgZl**2 + Fl**2)
   ZlonD =             self%BioC(11) * self%prefZlD * det**2/(self%RgZl**2 + Fl**2)
   ZlonZs = mic_loss * self%BioC(13) * self%prefZlZs * microzoo**2/(self%RgZl**2 + Fl**2)
   ! EXPERIMENT

   if (self%use_cyanos) then
     !ZsonBg = bg_loss  * self%BioC(31) * self%prefZsBG * bg/(self%BioC(14) + Fs)
     !ZlonBg = bg_loss  * self%BioC(31) * self%prefZlBG * bg/(self%BioC(14) + Fl)
     ZsonBg = bg_loss  * self%BioC(31) * self%prefZsBG * bg**2/(self%BioC(14)**2 + Fs**2)
     ZlonBg = bg_loss  * self%BioC(31) * self%prefZlBG * bg**2/(self%RgZl**2 + Fl**2)
   else
     ZsonBg=0.0_rk
     ZlonBg=0.0_rk
   end if

   if (self%use_coccolithophores) then
    !ZsonCocco = cocco_loss  * self%GrZsCocco * self%prefZsCocco * cocco/(self%BioC(14) + Fs)
    !ZlonCocco = cocco_loss  * self%GrZlCocco * self%prefZlCocco * cocco/(self%BioC(14) + Fl)
    ZsonCocco = cocco_loss  * self%GrZsCocco * self%prefZsCocco * cocco**2/(self%BioC(14)**2 + Fs**2)
    ZlonCocco = cocco_loss  * self%GrZlCocco * self%prefZlCocco * cocco**2/(self%RgZl**2 + Fl**2)
   else
    ZsonCocco=0.0_rk
    ZlonCocco=0.0_rk
   end if   

   ! nitrification
   Onitr = 0.01_rk * redf(7) !according to Neumann  (Onitr in mlO2/l see also Stigebrand and Wulff)
   bioom1 = 0.0_rk
   bioom2 = 0.0_rk
   bioom3 = 0.0_rk
   bioom4 = 0.0_rk
   bioom5 = 0.0_rk
   bioom6 = 0.0_rk
   bioom7 = 0.0_rk
   bioom8 = 0.0_rk
   if (oxy > 0) then
     bioom1 = 0.1_rk/secs_pr_day * exp(temp*0.11_rk) * oxy/(Onitr+oxy)
     bioom2 = bioom1
     bioom6 = 1.0_rk
   else
     if (no3>0) then
       bioom5 = 5.0_rk
       bioom8 = 1.0_rk
     else
       bioom7 = 1.0_rk
     end if
   end if

! reaction rates
   highMortPs = self%m2Ps * ( fla/(fla + self%Km2Ps) )  
   highMortPl = self%m2Pl * ( dia/(dia + self%Km2Pl) )
   highMortZs = self%m2Zs * ( microzoo/(microzoo + self%Km2Zs) )
   highMortZl = self%m2Zl * ( mesozoo/(mesozoo + self%Km2Zl) )
   if (self%use_coccolithophores) then 
      highMortCocco = self%mort2Cocco * ( cocco/(cocco + self%Km2Cocco) )
   end if
   _SET_ODE_(self%id_fla, (self%BioC(2)*Ps_prod - self%BioC(10)*fla_loss - highMortPs*fla_loss )*fla - ZsonPs*microzoo - ZlonPs*mesozoo)
   _SET_ODE_(self%id_dia, (self%BioC(1)*Pl_prod - self%BioC(9)*dia_loss - highMortPl*dia_loss )*dia - ZsonPl*microzoo - ZlonPl*mesozoo)
   if (self%use_cyanos) then
     _SET_ODE_(self%id_bg,  (self%BioC(28)*(Bg_prod + Bg_fix) - self%BioC(32)*bg_loss)*bg - ZsonBg*microzoo - ZlonBg*mesozoo)
   end if
   if (self%use_coccolithophores) then
     _SET_ODE_(self%id_cocco, (self%muCocco*Cocco_prod - self%mortCocco*cocco_loss  - highMortCocco*cocco_loss )*cocco - ZsonCocco*microzoo - ZlonCocco*mesozoo)
   end if

  ! for chlorophyll-a
   if (self%use_chl) then
     rhs = self%BioC(2)*Ps_prod*chl2c_fla*fla - ( ( (self%BioC(10) + highMortPs) * fla_loss * fla + ZsonPs*microzoo + ZlonPs*mesozoo)*flachl/fla )
     _SET_ODE_(self%id_flachl,rhs)
     rhs = self%BioC(1)*Pl_prod*chl2c_dia*dia - ( ( (self%BioC(9) + highMortPl) * dia_loss * dia + ZsonPl*microzoo + ZlonPl*mesozoo)*diachl/dia )
     _SET_ODE_(self%id_diachl,rhs)
     if (self%use_cyanos) then
       rhs = self%BioC(28)*(Bg_prod + Bg_fix)*chl2c_bg*bg - ((self%BioC(32)*bg*bg_loss + ZsonBg*microzoo + ZlonBg*mesozoo)*bgchl/bg )
       _SET_ODE_(self%id_bgchl,rhs)
     end if 
     if (self%use_coccolithophores) then
       rhs = self%muCocco*Cocco_prod*chl2c_cocco*cocco - ( ( (self%mortCocco + highMortCocco) * cocco_loss * cocco + ZsonCocco*microzoo + ZlonCocco*mesozoo)*coccochl/cocco )
       _SET_ODE_(self%id_coccochl,rhs)
     end if
   end if

   ! microzooplankton

   Zs_prod = self%BioC(20)*(ZsonPs + ZsonPl + ZsonBg + ZsonCocco) + self%BioC(21)*ZsonD
   rhs = (Zs_prod - (self%BioC(16) + self%BioC(18) + highMortZs + self%zpr)*mic_loss) * microzoo &
         - ZlonZs * mesozoo
   _SET_ODE_(self%id_microzoo, rhs)

   ! mesozooplankton
   Zl_prod = self%BioC(19)*(ZlonPs + ZlonPl + ZlonBg + ZlonZs + ZlonCocco) + self%BioC(21)*ZlonD
   rhs = (Zl_prod - (self%BioC(15) + self%BioC(17) + highMortZl + self%zpr)*mes_loss) * mesozoo
   _SET_ODE_(self%id_mesozoo, rhs)

   ! detritus
   dxxdet = (  ((1.0_rk-self%BioC(20))*(ZsonPs + ZsonPl + ZsonBg + ZsonCocco) &
              + (1.0_rk-self%BioC(21))*ZsonD) * microzoo &
              + ((1.0_rk-self%BioC(19))*(ZlonPs + ZlonPl + ZlonBg + ZlonZs + ZlonCocco) &
              + (1.0_rk-self%BioC(21))*ZlonD) * mesozoo &
              + (self%BioC(16) + highMortZs) * microzoo * mic_loss &
              + (self%BioC(15) + highMortZl) * mesozoo * mes_loss &
              + (self%BioC(10) + highMortPs) * fla * fla_loss &
              + (self%BioC(9) + highMortPl)  * dia * dia_loss)
   if (self%use_cyanos) then
       dxxdet = dxxdet + (self%BioC(32) * bg * bg_loss )
   end if
   if (self%use_coccolithophores) then
       dxxdet = dxxdet + (self%mortCocco + highMortCocco) * cocco * cocco_loss  
   end if

   rhs = (1.0_rk-self%frr) * dxxdet &
         - ZsonD * microzoo &
         - ZlonD * mesozoo &
         - frem * det
   _SET_ODE_(self%id_det, rhs)
   if (self%use_community_sinking) then
    ! community dependent sinking rate
    ! 
    ! dsnk/det is the calculated (and applied) detritus sinking speed
    ! assumptions: unassimilated food uses detritus sinking rates from the prey,
    ! not the predator.
       
      sinkdxxdet = (  ((1.0_rk-self%BioC(20))*(ZsonPs * self%sinkFlaD + ZsonPl * self%sinkDiaD + ZsonBg * self%sinkBgD + ZsonCocco * self%sinkCoccoD) &
               + (1.0_rk-self%BioC(21)) * ZsonD * dsnk/det) * microzoo &
               + ((1.0_rk-self%BioC(19))*(ZlonPs * self%sinkFlaD + ZlonPl * self%sinkDiaD + ZlonBg * self%sinkBgD + ZlonZs * self%sinkMicD + ZlonCocco * self%sinkCoccoD) &
               + (1.0_rk-self%BioC(21)) * ZlonD * dsnk/det) * mesozoo &
               + (self%BioC(16) + highMortZs) * microzoo * mic_loss * self%sinkMicD &
               + (self%BioC(15) + highMortZl) * mesozoo * mes_loss * self%sinkMesD &
               + (self%BioC(10) + highMortPs) * fla * fla_loss * self%sinkFlaD &
               + (self%BioC(9) + highMortPl)  * dia * dia_loss * self%sinkDiaD )
      if (self%use_cyanos) then
               sinkdxxdet = sinkdxxdet + (self%BioC(32) * bg * bg_loss * self%sinkBgD)
      end if
      if (self%use_coccolithophores) then
          sinkdxxdet = sinkdxxdet + (self%mortCocco + highMortCocco) * cocco * cocco_loss * self%sinkCoccoD 
      end if
        
      rhs = (1.0_rk-self%frr) * sinkdxxdet &
            - ZsonD * microzoo * dsnk/det &
            - ZlonD * mesozoo * dsnk/det &
              - frem * dsnk
      
      _SET_ODE_(self%id_dsnk, rhs)

!      if (dsnk/det*86400. .lt. 5.0*0.95 .or. dsnk/det*86400. .gt. 5.0*1.05) then
!         write(*,*) "BAD SINKING VALUE",dsnk/det*86400.
!      end if

   endif

   ! labile dissolved organic matter
   _SET_ODE_(self%id_dom, self%frr*dxxdet - fremDOM * dom)

   ! nitrate
   rhs_nit = -(up_no3+0.5d-10)/(up_n+1.0d-10)*(Prod_Dia_Fla + Prod_BG) &
         - (up_no3_cocco+0.5d-10)/(up_n_cocco+1.0d-10)*Prod_Cocco &
         + bioom1 * nh4 &
         - bioom3 * no3 &
         - frem * det * bioom5 &
         - fremDOM * dom * bioOM5
   _SET_ODE_(self%id_no3, rhs_nit)

   ! ammonium
   rhs_amm = -(up_nh4+0.5d-10)/(up_n+1.0d-10)*(Prod_Dia_Fla + Prod_BG) &
         - (up_nh4_cocco+0.5d-10)/(up_n_cocco+1.0d-10)*Prod_Cocco &
         + self%BioC(18) * microzoo * mic_loss &
         + self%BioC(17) * mesozoo * mes_loss &
         + frem * det &
         + fremDOM * dom - bioom1 * nh4
   _SET_ODE_(self%id_nh4, rhs_amm)

   ! phosphate

   rhs = -Prod_Dia_Fla -Prod_Cocco -Prod_BG &
         + self%BioC(18) * microzoo * mic_loss &
         + self%BioC(17) * mesozoo * mes_loss &
         + frem*det + fremDOM*dom
   if (self%use_cyanos) then
      rhs = rhs - self%BioC(28)*bg*Bg_fix
   end if
   _SET_ODE_(self%id_pho, rhs)


   ! silicate
   _SET_ODE_(self%id_sil, -self%BioC(1)*Pl_prod*dia + self%BioC(27)*opa)

   ! opal
   _SET_ODE_(self%id_opa, (self%BioC(9) + highMortPl)*dia*dia_loss + ZsonPl*microzoo + ZlonPl*mesozoo - self%BioC(27)*opa)

   ! oxygen
   rhs_oxy = ((6.625*up_nh4 + 8.125*up_no3+1.d-10)/(up_n+1.d-10)*(Prod_Dia_Fla + Prod_BG) &
         +(6.625*up_nh4_cocco + 8.125*up_no3_cocco+1.d-10)/(up_n_cocco+1.d-10)*Prod_Cocco &
         -bioom6*6.625*(self%BioC(18)*microzoo*mic_loss &
         +self%BioC(17)*mesozoo*mes_loss) &
         -frem*det*(bioom6+bioom7)*6.625 &
         -(bioom6+bioom7)*6.625*fremDOM*dom &
         -2.0_rk*bioom1*nh4)*redf(11)*redf(16)
   _SET_ODE_(self%id_oxy, rhs_oxy)

   ! CaCO3
   if (self%use_coccolithophores) then
    !  Rstar = self%calcR * Cocco_prod * self%incCarbR * max(0.0001,temp/(2.0 + temp)) * max(1.0,0.5*cocco*redf(16)*redf(11)) 
      Rstar = self%calcR * Cocco_prod * max(0.0001,temp/(2.0 + temp)) * max(1.0,0.5*cocco*redf(16)*redf(11))
!      Pcaco3 = Rstar * ( (self%muCocco*Cocco_prod - 0.5 * self%mortCocco*cocco_loss*caco3_loss)*cocco - 0.5 * ZsonCocco*microzoo*caco3_loss - 0.5 * ZlonCocco*mesozoo*caco3_loss )
      Pcaco3 = Rstar * ( (self%muCocco*Cocco_prod - 0.5 *self%mortCocco*cocco_loss -0.5*highMortCocco*cocco_loss )*cocco - 0.5 *ZsonCocco*microzoo - 0.5 * ZlonCocco*mesozoo )
      Lstar = self%calcDis * max(0.0,1.0 - Om_cal)
!      if (Lstar.gt.0.0) then
!        write(*,*)"Lstar negative",Lstar,self%calcDis,Om_cal
!      endif
!      if (Pcaco3.gt.0.0) then
!        write(*,*)"Pstar positive",Rstar
!      endif
      !caco3_loss = max(sign(-1.0_rk,caco3-0.0001_rk),0.0_rk) ! caco3 - temporary for now
!      if (caco3_loss.eq.0 .and. Pcaco3.lt.0.0) then
!        rhs = 0.
!        write(*,*)"HERE"
!      else
!      rhs = Pcaco3 - Lstar * caco3 * caco3_loss
      rhs = Pcaco3 - Lstar * caco3
!      end if
      !write(*,*)rhs*480.,caco3,caco3+rhs*480.
      _SET_ODE_(self%id_caco3, rhs) 
   end if 

   ! Carbonate dynamics
   if (self%couple_co2) then
     rhs =  redf(16) *( self%BioC(18)*microzoo*mic_loss &
            + self%BioC(17)*mesozoo*mes_loss &
            + frem*det + fremDOM*dom - Prod_Dia_Fla - Prod_BG) 
     if (self%use_coccolithophores) then
!        rhs = rhs - ( Prod_Cocco * self%incCarbR ) + Pcaco3 - Lstar * caco3
        rhs = rhs - Prod_Cocco * redf(16) - Pcaco3 + Lstar * caco3 !* caco3_loss
     endif
     _SET_ODE_(self%id_dic, rhs)

     rhs = redf(16)*(rhs_amm-rhs_nit)*redf(11) - 0.5_rk*rhs_oxy*(1._rk-bioom6)
     if (self%use_coccolithophores) then
!        rhs = rhs + redf(16) * (- Pcaco3 + Lstar * caco3)
!        rhs = rhs + redf(16) * 2. * (- Pcaco3 + Lstar * caco3 * caco3_loss)
        rhs = rhs + redf(16) * 2. * (- Pcaco3 + Lstar * caco3)
     endif
     _SET_ODE_(self%id_alk, rhs)
   end if

   ! Export diagnostic variables

   ! For net primary production, we assume 10% of phyto piomass as respiration
   sumgpp = Prod_Dia_Fla
   sumnetpp = Prod_Dia_Fla - 0.1_rk/sedy0 * (dia + fla)
   if (self%use_cyanos) then
      sumgpp = sumgpp + Prod_BG + self%BioC(28)*bg*Bg_fix
      sumnetpp = sumnetpp - 0.1_rk/sedy0 * bg
   end if
   if (self%use_coccolithophores) then
      sumgpp = sumgpp + Prod_Cocco
      sumnetpp = sumnetpp - 0.1_rk/sedy0 * cocco
   end if
      
!   _SET_DIAGNOSTIC_(self%id_primprod, Prod_Dia_Fla + Prod_Cocco + Prod_BG + self%BioC(28)*bg*Bg_fix )      
   _SET_DIAGNOSTIC_(self%id_primprod, sumgpp )
   _SET_DIAGNOSTIC_(self%id_netpp, sumnetpp )
   _SET_DIAGNOSTIC_(self%id_secprod, Zl_prod*mesozoo + Zs_prod*microzoo)

   if (self%turn_on_additional_diagnostics) then
    _SET_DIAGNOSTIC_(self%id_parmean_diag, mean_par)
    _SET_DIAGNOSTIC_(self%id_denit,(frem*det*bioom5+fremDOM*dom*bioom5)*redf(11)*redf(16))

    ! CAGLAR: diagnostics nutrient limitation is not complete, it does not include coccoliths
    ! We need a phyto specific definitions of uptake rates
    _SET_DIAGNOSTIC_(self%id_nlim, up_n)
    _SET_DIAGNOSTIC_(self%id_plim, up_pho)
    _SET_DIAGNOSTIC_(self%id_slim, up_sil)
    if (self%use_chl_in_PI_curve) then
      if (self%use_coccolithophores) then
         _SET_DIAGNOSTIC_(self%id_llim, (blightFLA + blightDIA + blightCOCCO)/3.)
      else
         _SET_DIAGNOSTIC_(self%id_llim, (blightFLA + blightDIA)/2.) ! cyanobacteria is not included here
      end if
    else
      _SET_DIAGNOSTIC_(self%id_llim, blight)
    end if

    if (self%use_community_sinking) then
        _SET_DIAGNOSTIC_(self%id_snkspd,dsnk/det*sedy0) ! set the output to be (m d-1)
    end if

    if (self%use_chl) then
       _SET_DIAGNOSTIC_(self%id_c2chl_fla, 1.0_rk/chl2c_fla)
       _SET_DIAGNOSTIC_(self%id_c2chl_dia, 1.0_rk/chl2c_dia)
       if (self%use_cyanos) then
         _SET_DIAGNOSTIC_(self%id_c2chl_bg, 1.0_rk/chl2c_bg)
       end if
       if (self%use_coccolithophores) then
         _SET_DIAGNOSTIC_(self%id_c2chl_cocco, 1.0_rk/chl2c_cocco)
       end if
    end if
   end if
   _LOOP_END_

   end subroutine do
!EOC

!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: Surface fluxes for the ecosmo model
!
! !INTERFACE:

   subroutine do_surface(self,_ARGUMENTS_DO_SURFACE_)
   class (type_nersc_ecosmo),intent(in) :: self
   _DECLARE_ARGUMENTS_DO_SURFACE_
!
! !LOCAL VARIABLES:
   real(rk) :: o2flux, T, tr, S, o2sat, oxy
   real(rk) :: no3flux, phoflux
   real(rk) :: pho,par,bg,blight,tbg,up_pho,prod
!EOP
!-----------------------------------------------------------------------
!BOC
   _HORIZONTAL_LOOP_BEGIN_

   _GET_(self%id_temp,T)
   _GET_(self%id_salt,S)
   _GET_(self%id_oxy,oxy)
   _GET_(self%id_par,par)
   _GET_(self%id_pho,pho)
   if (self%use_cyanos) then
     _GET_(self%id_bg,bg)
   else
     bg=0.0_rk
   end if

   ! Oxygen saturation micromol/liter__(Benson and Krause, 1984)
   tr = 1.0_rk/(T + 273.15_rk)
   o2sat= exp(- 135.90205_rk              &
       + (1.575701d05 ) * tr               &
       - (6.642308d07 ) * tr**2            &
       + (1.243800d10) * tr**3            &
       - (8.621949d11) * tr**4            &
       - S*(0.017674_rk-10.754_rk*tr+2140.7_rk*tr**2)  )

!   o2flux = 5._rk/secs_pr_day * (o2sat - oxy)
   o2flux = 1._rk/secs_pr_day * (o2sat - oxy)

   _SET_SURFACE_EXCHANGE_(self%id_oxy,o2flux)

   _SET_SURFACE_EXCHANGE_(self%id_no3,self%surface_deposition_no3)
   _SET_SURFACE_EXCHANGE_(self%id_nh4,self%surface_deposition_nh4)
   _SET_SURFACE_EXCHANGE_(self%id_pho,self%surface_deposition_pho)
   _SET_SURFACE_EXCHANGE_(self%id_sil,self%surface_deposition_sil)

! CAGLAR: note to myself : figure out the false if condition below, who and why   
#if 0 
   if (self%use_cyanos) then
     ! calculate cyanobacteria surface production
     if (S <= 10.0) then
       tbg = 1.0_rk/(1.0_rk + exp(self%BioC(29)*(self%BioC(30)-T)))
     else
       tbg = 0.0_rk
     end if

     blight=max(tanh(self%BioC(3)*par),0.)
     up_pho = pho/(self%BioC(25)+pho)
     prod = self%BioC(28) * bg * Tbg * min(blight, up_pho) ! cyanobacteria production

   !_SET_ODE_(self%id_bg,  prod)
   !_SET_ODE_(self%id_pho, -prod)
   !_SET_SURFACE_ODE_(id_oxy, ) ! not included in the modular ECOSMO version
   !_SET_SURFACE_ODE_(id_dic, -Prod)
   end if
#endif

   ! Leave spatial loops over the horizontal domain (if any)
   _HORIZONTAL_LOOP_END_

   end subroutine do_surface
!EOC

!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: Bottom fluxes for the ecosmo model
!
! !INTERFACE:

   subroutine do_bottom(self,_ARGUMENTS_DO_BOTTOM_)
   class (type_nersc_ecosmo),intent(in) :: self
   _DECLARE_ARGUMENTS_DO_BOTTOM_
!
! !LOCAL VARIABLES:
   real(rk) :: temp, tbs, oxy, no3, det, opa, sed1, sed2, sed3
   real(rk) :: pho, Rds, Rsd, Rsa, Rsdenit, Rsa_p, yt1, yt2
   real(rk) :: rhs, flux, alk_flux
   real(rk) :: bioom1, bioom2, bioom3, bioom4, bioom5, bioom6, bioom7, bioom8
   ! add community sinking local variables
   real(rk) :: dsnk
   ! add coccolithophores + caco3 variables
   real(rk) :: caco3, sed4
!   real(rk) :: caco3_loss=1.0_rk
!EOP
!-----------------------------------------------------------------------
!BOC
   _HORIZONTAL_LOOP_BEGIN_

   _GET_(self%id_temp,temp)
   _GET_(self%id_oxy,oxy)
   _GET_(self%id_det,det)
   _GET_(self%id_opa,opa)
   _GET_(self%id_no3,no3)
   if (self%use_community_sinking) then
     _GET_(self%id_dsnk,dsnk)
   end if
   if (self%use_coccolithophores) then
    _GET_(self%id_caco3,caco3)
   endif
   _GET_HORIZONTAL_(self%id_sed1,sed1)
   _GET_HORIZONTAL_(self%id_sed2,sed2)
   _GET_HORIZONTAL_(self%id_sed3,sed3)
   if (self%use_coccolithophores) then
    _GET_HORIZONTAL_(self%id_sed4,sed4)
   endif
   _GET_HORIZONTAL_(self%id_tbs,tbs)

!   sed1 = max(sed1,0.0_rk)
!   sed2 = max(sed2,0.0_rk)
!   sed3 = max(sed3,0.0_rk)
!   if (self%use_coccolithophores) then
!      sed4 = max(sed4,0.0_rk)
!   end if

   bioom1 = 0.0_rk
   bioom2 = 0.0_rk
   bioom3 = 0.0_rk
   bioom4 = 0.0_rk
   bioom5 = 0.0_rk
   bioom6 = 0.0_rk
   bioom7 = 0.0_rk
   bioom8 = 0.0_rk
   if (oxy > 0) then
     bioom1 = 0.1/secs_pr_day * exp(temp*0.11_rk) * oxy/((0.1_rk*redf(7))+oxy)
     bioom2 = bioom1
     bioom6 = 1.0_rk
   else
     if (no3>0) then
       bioom5 = 5.0_rk
       bioom8 = 1.0_rk
     else
       bioom7 = 1.0_rk
     end if
   end if

!----citical bottom shear stress
        if (tbs.ge.self%BioC(34)) then
          Rsd=min(self%BioC(35), self%BioC(35) * (tbs-0.1)**2 * 100.) ! sets to max=self%BioC(35) when tbs=0.2,
                 ! sets to max=self%BioC(35) when tbs=0.2,
                 ! else rapid increase to max when tbs=0.1
                 ! it assumes BioC(34) = 0.1 in the fabm.yaml file
          Rds=0.0_rk
        else if (tbs.lt.self%BioC(34)) then
          Rsd=0.0_rk
          Rds=self%BioC(36)
          if (self%use_community_sinking) then
            Rds=dsnk/det
          end if
        end if

!---------------------------------------------------------------
!----denitrification parameter in dependence of available oxygen
        if (oxy .gt. 0.0) then
          Rsa=self%BioC(38)*exp(self%BioC(39)*temp)*1.0_rk
          Rsdenit=0.0_rk
        else if (oxy .le. 0.0) then
          Rsdenit=self%BioC(38)*exp(self%BioC(39)*temp)*2.0_rk
          Rsa=0.0_rk
        end if

        !--- sediment 1 total sediment biomass and nitrogen pool
        rhs = Rds*det - Rsd*sed1 - 2.0_rk*Rsa*sed1 - Rsdenit*sed1 &
              -(2.0E-3*self%BioC(37)*sed1)*sed1 !- self%BioC(37)*sed1
        _SET_BOTTOM_ODE_(self%id_sed1, rhs)

        ! community sinking variable exchange
        if (self%use_community_sinking) then
          _SET_BOTTOM_EXCHANGE_(self%id_dsnk, Rsd*sed1*dsnk/det - Rds*det*dsnk/det)
         end if

        ! oxygen
        flux = -(BioOM6*6.625_rk*2.0_rk*Rsa*sed1 &
                 +BioOM7*6.625_rk*Rsdenit*sed1 &
                 +2.0_rk*BioOM1*Rsa*sed1) &
                *REDF(11)*REDF(16)
        _SET_BOTTOM_EXCHANGE_(self%id_oxy, flux)

        ! nitrate
        _SET_BOTTOM_EXCHANGE_(self%id_no3, -BioOM5*Rsdenit*sed1)

        ! detritus
        _SET_BOTTOM_EXCHANGE_(self%id_det, Rsd*sed1 - Rds*det)

        ! ammonium
        _SET_BOTTOM_EXCHANGE_(self%id_nh4, (Rsdenit+Rsa)*sed1)

        ! CaCO3
        if (self%use_coccolithophores) then
!          caco3_loss = max(sign(-1.0_rk,caco3-0.0001_rk),0.0_rk) ! caco3 - temporary for now
!          rhs = Rds*caco3*caco3_loss - Rsd*sed4 - self%BioC(42)*sed4 &
!                -(2.0E-3*self%BioC(37)*sed4)*sed4 
!          _SET_BOTTOM_ODE_(self%id_sed4, rhs)
!          _SET_BOTTOM_EXCHANGE_(self%id_caco3, Rsd*sed4 - Rds*caco3*caco3_loss) 
          rhs = Rds*caco3 - Rsd*sed4 - self%BioC(42)*sed4 &
                -(2.0E-3*self%BioC(37)*sed4)*sed4 
          _SET_BOTTOM_ODE_(self%id_sed4, rhs)
          _SET_BOTTOM_EXCHANGE_(self%id_caco3, Rsd*sed4 - Rds*caco3)
        end if

        if (self%couple_co2) then
          _SET_BOTTOM_EXCHANGE_(self%id_dic, redf(16)*(Rsdenit+2*Rsa)*sed1)
          alk_flux = redf(16)*redf(11)*((Rsdenit+Rsa+bioom5*Rsdenit)*sed1) - 0.5_rk*flux*(1._rk-bioom6)
          _SET_BOTTOM_EXCHANGE_(self%id_alk, alk_flux)
        end if
        !--try out for phosphate ute 2.6.2010
        Rsa_p=self%BioC(38)*exp(self%BioC(39)*temp)*2.0_rk

        if (oxy.gt.0.0) then
          yt2=oxy/375.0_rk   !normieren des wertes wie in Neumann et al 2002
          yt1=yt2**2.0_rk/(self%BioC(41)**2.0_rk+yt2**2.0_rk)

          _SET_BOTTOM_EXCHANGE_(self%id_pho,Rsa_p*(1.0_rk-self%BioC(40)*yt1)*sed3)

          !--sed 3 phosphate pool sediment+remineralization-P release
          _SET_BOTTOM_ODE_(self%id_sed3, 2.0_rk*Rsa*sed1 - Rsa_p*(1.0_rk-self%BioC(40)*yt1)*sed3)

        else if (oxy.le.0.0) then
          _SET_BOTTOM_EXCHANGE_(self%id_pho, Rsa_p*sed3)
          _SET_BOTTOM_ODE_(self%id_sed3, Rsdenit*sed1 - Rsa_p*sed3)
        end if

        ! sediment opal(Si)
!        _SET_BOTTOM_ODE_(self%id_sed2, Rds*opa - Rsd*sed2 - self%BioC(42)*sed2 - ( self%BioC(37)*1000.*(sed2**3/(sed2**3 + 1E+12)) )*sed2)
        _SET_BOTTOM_ODE_(self%id_sed2, 2.0*Rds*opa - Rsd*sed2 - self%BioC(42)*sed2 - (2.0E-3*self%BioC(37)*sed2)*sed2)
        _SET_BOTTOM_EXCHANGE_(self%id_opa, Rsd*sed2 - 2.0*Rds*opa)
!        _SET_BOTTOM_EXCHANGE_(self%id_opa, Rsd*sed2 - Rds*opa)
        _SET_BOTTOM_EXCHANGE_(self%id_sil, self%BioC(42)*sed2)

        if (self%turn_on_additional_diagnostics) then
           _SET_HORIZONTAL_DIAGNOSTIC_(self%id_tbsout, tbs)
        endif
   _HORIZONTAL_LOOP_END_

   end subroutine do_bottom
!EOC

   subroutine get_light_extinction(self,_ARGUMENTS_GET_EXTINCTION_)
   class (type_nersc_ecosmo), intent(in) :: self
   _DECLARE_ARGUMENTS_GET_EXTINCTION_

   real(rk)                     :: dom, det, diachl, flachl, bgchl, coccochl
   real(rk)                     :: dia, fla, bg, cocco
   real(rk)                     :: my_extinction

   ! Enter spatial loops (if any)
   _LOOP_BEGIN_

   ! Retrieve current (local) state variable values.

   _GET_(self%id_det, det)
   _GET_(self%id_dom, dom)
   _GET_(self%id_dia, dia)
   _GET_(self%id_fla, fla)
   if (self%use_cyanos) then
      _GET_(self%id_bg, bg)
   else
      bg = 0.0_rk
   end if
   if (self%use_coccolithophores) then
      _GET_(self%id_cocco, cocco)
   else
      cocco = 0.0_rk
   end if 

   my_extinction = self%BioC(4)
   if (self%use_chl) then
     _GET_(self%id_diachl, diachl)
     _GET_(self%id_flachl, flachl)
     if (self%use_cyanos) then
       _GET_(self%id_bgchl, bgchl)
     else
       bgchl = 0.0_rk
     end if
     if (self%use_coccolithophores) then
       _GET_(self%id_coccochl, coccochl)
     else
       coccochl = 0.0_rk 
     end if
     my_extinction = my_extinction + self%BioC(5)*(diachl+flachl+bgchl+coccochl) + self%extdet*det + self%extdom*dom
   else
     diachl = 0.0_rk
     flachl = 0.0_rk
     coccochl = 0.0_rk
     my_extinction = my_extinction + self%BioC(5)*(dia+fla+bg+cocco) + self%extdet*det + self%extdom*dom
   end if

   _SET_EXTINCTION_( my_extinction )

   ! Leave spatial loops (if any)
   _LOOP_END_

   end subroutine get_light_extinction

! ----- COMMUNITY DEPENDENT VARIABLE SINKING ------------------------------
   subroutine get_vertical_movement(self,_ARGUMENTS_GET_VERTICAL_MOVEMENT_)
    
    class (type_nersc_ecosmo),intent(in) :: self
    _DECLARE_ARGUMENTS_GET_VERTICAL_MOVEMENT_

    real(rk) :: det, dsnk, meanspd, minspd, maxspd

    _LOOP_BEGIN_
       if (self%use_community_sinking) then
         _GET_(self%id_det, det)
         _GET_(self%id_dsnk, dsnk)

         meanspd = dsnk / det
!         ! Below is necessary for HYCOM. For GOTM runs, this is redundant. The
!         ! community sinking is quite conservative, but with HYCOM
!         ! vanishing/reappearing layers, it is very unstable. So below replaces
!         ! the sinking speed to initial detritus sinking speed given in
!         ! fabm.yaml file. For now any value outside the prescribed minimum and
!         ! maximum will be replaced, and dsnk variable modified accordingly.
!         minspd = min( self%sinkDiaD, self%sinkFlaD, self%sinkMicD, self%sinkMesD, self%sinkCoccoD)
!         maxspd = max( self%sinkDiaD, self%sinkFlaD, self%sinkMicD,self%sinkMesD, self%sinkCoccoD)
!         if ( meanspd .lt. minspd .or. meanspd .gt. maxspd) then
!            meanspd = self%BioC(23)
!            _SET_(self%id_dsnk, meanspd * det)
!         end if
         
         _SET_VERTICAL_MOVEMENT_(self%id_det,-meanspd)
         _SET_VERTICAL_MOVEMENT_(self%id_dsnk,-meanspd)
         _SET_VERTICAL_MOVEMENT_(self%id_opa,-meanspd)
         if (self%use_coccolithophores) then
!          _SET_VERTICAL_MOVEMENT_(self%id_caco3,-self%sinkCoccoD) ! set CaCO3 sinking to Coccolith detritus sinking
          _SET_VERTICAL_MOVEMENT_(self%id_caco3,-meanspd)
         end if 
        else
         _SET_VERTICAL_MOVEMENT_(self%id_det,-self%BioC(23))
         _SET_VERTICAL_MOVEMENT_(self%id_opa,-self%BioC(43))
         if (self%use_coccolithophores) then
            _SET_VERTICAL_MOVEMENT_(self%id_caco3,-self%BioC(43)) ! set CaCO3 sinking to Opal
         end if 
       endif
       _LOOP_END_
 end subroutine get_vertical_movement
! -------------------------------------------------------------------------

   end module fabm_nersc_ecosmo


